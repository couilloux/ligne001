<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.0.26">
  <POU Name="ST2_ASPIRATION" Id="{d8a1094e-ee3f-4bf2-b079-46d632c044ba}">
    <Declaration><![CDATA[PROGRAM ST2_ASPIRATION
VAR
	STEP: INT := 0;
	JIG_STATUS_R_TRIG: R_TRIG;
	TAMPO_AMORCAGE_TON: TON;
	START_TAMPO_AMORCAGE: BOOL;
	TAMPO_MESURE_ASP_TON: TON;
	START_TAMPO_MESURE_ASP: BOOL;
	TAMPO_MESURE_SOUPAPES_TON: TON;
	START_TAMPO_MESURE_SOUPAPES: BOOL;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[(*******************************************************
*
* Station 2 - Aspiration
*
*
*********************************************************)
IF NOT(GEMMA.STEP='F1') THEN
	STEP:=0;
END_IF;


CASE STEP OF

0: (*Init*)

	ST2_2V4 := FALSE;
	ST2_2V5 := FALSE;
	ST2_2V6 := FALSE;
	ST2_2V7 := FALSE;
	ST2_2V8 := FALSE;
	
	FLAGS.ST2_RELEASE_CARRIER := FALSE;
	START_TAMPO_AMORCAGE := FALSE;
	START_TAMPO_MESURE_ASP := FALSE;
	START_TAMPO_MESURE_SOUPAPES := FALSE;
	
	STEP := STEP + 10;
	
10: (*Attend une palette / flanc montant du status*)

	IF JIG_STATUS_R_TRIG.Q THEN
		STEP := STEP + 10;
	END_IF;
	
20: (*Descend le vérin du dessus*)

	ST2_2V4 := TRUE;
	STEP := STEP + 10;
	
30: (*Attend vérin dessus sorti*)

	IF ST2_2B4 THEN
		STEP := STEP + 10;
	END_IF;

40: (*Monte le vérin du dessous*)

	ST2_2V5 := TRUE;
	STEP := STEP + 10;
	
50: (*Attend vérin dessus sorti*)

	IF ST2_2B5 THEN
		STEP := STEP + 10;
	END_IF;
	
60: (*Sort le vérin électrique*)

	ST2_2V6 := TRUE;
	STEP := STEP + 10;
	
70: (*Attend vérin électrique sortie*)

	IF ST2_2B6 THEN
		STEP := STEP + 10;
	END_IF;

80: (*Allume la pompe*)

	IF PUMP_PARAMS.ST2_CONTRE_PRESSION THEN (*ouvre la vanne de contre pression d'air si nécessaire*)
		ST2_2V8:=TRUE;
	END_IF;
	
	ST2_POWER := TRUE;
	STEP := 100;
	
(**************************************************************************)
100: (*Démarre la tampo pour l'amorcage*)

	START_TAMPO_AMORCAGE := TRUE;
	STEP := STEP + 10;
	
110: (*Attend fin tampo amorcage*)

	IF TAMPO_AMORCAGE_TON.Q THEN
		START_TAMPO_AMORCAGE := FALSE;
		STEP := STEP + 10;
	END_IF;
	
120: (*Démarre la tampo pour la mesure de pression*)

	START_TAMPO_MESURE_ASP := TRUE;
	STEP := STEP + 10;
	
130: (*Attend fin tampo mesure - MESURE EN COURT*)

	IF TAMPO_MESURE_ASP_TON.Q THEN
		START_TAMPO_MESURE_ASP := FALSE;
		STEP := STEP + 10;
	END_IF;
	
140: (*Démarre la tampo pour la mesure d'étanchéité des soupapes*)

	START_TAMPO_MESURE_SOUPAPES := TRUE;
	ST2_POWER := FALSE;
	STEP := STEP + 10;
	
150: (*Attend fin tampo mesure étanchéité des soupapes - MESURE EN COURT*)

	IF TAMPO_MESURE_SOUPAPES_TON.Q THEN
		START_TAMPO_MESURE_SOUPAPES:= FALSE;
		ST2_2V8:=FALSE; (*ferme la vanne de contre pression d'air*)
		STEP := 300;
	END_IF;
	

(**************************************************************************)	

300: (*Rentre le vérin électrique*)

	ST2_2V6 := FALSE;
	STEP := STEP + 10;
	
310: (*Descend le vérin du dessous*)

	ST2_2V5 := FALSE;
	STEP := STEP + 10;
	
320: (*Monte le vérin du dessus*)

	ST2_2V4 := FALSE;
	STEP := STEP + 10;

330: (*Libère la palette*)

	FLAGS.ST2_RELEASE_CARRIER := TRUE;
	STEP := 0;


ELSE
    (*default case*);
END_CASE;

(*FBs*)
TAMPO_AMORCAGE_TON(IN:= START_TAMPO_AMORCAGE, PT:= PUMP_PARAMS.ST2_TEMPS_POMPAGE);
TAMPO_MESURE_ASP_TON(IN:= START_TAMPO_MESURE_ASP, PT:= PUMP_PARAMS.ST2_TEMPS_MESURE_ASPIRATION);
TAMPO_MESURE_SOUPAPES_TON(IN:= START_TAMPO_MESURE_SOUPAPES, PT:= PUMP_PARAMS.ST2_TEMPS_MESURE_ASPIRATION);
JIG_STATUS_R_TRIG(CLK:= CONVEYOR.JIG_ST2.STATUS);
PROG_STEPS.ST2:=STEP;]]></ST>
    </Implementation>
    <ObjectProperties />
  </POU>
</TcPlcObject>