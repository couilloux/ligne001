<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.0.26">
  <POU Name="ST2_ASPIRATION" Id="{d8a1094e-ee3f-4bf2-b079-46d632c044ba}">
    <Declaration><![CDATA[PROGRAM ST2_ASPIRATION
VAR
	STEP: INT := 0;
	JIG_STATUS_R_TRIG: R_TRIG;
	TAMPO_AMORCAGE_TON: TON;
	START_TAMPO_AMORCAGE: BOOL;
	TAMPO_MESURE_TON: TON;
	START_TAMPO_MESURE: BOOL;
	TAMPO_SORTIE_VERIN_HAUT_TON: TON;
	START_TAMPO_VERIN_HAUT: BOOL;
	TAMPO_SORTIE_VERIN_BAS_TON: TON;
	START_TAMPO_VERIN_BAS: BOOL;
	TAMPO_SORTIE_VERIN_ELEC_TON: TON;
	START_TAMPO_VERIN_ELEC: BOOL;
	START_TAMPO_MESURE_PERTES: BOOL;
	TAMPO_MESURE_PERTES_TON: TON;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[(*******************************************************
*
* Station 2 - Aspiration
*
*
*********************************************************)
IF NOT(GEMMA.STEP='F1') THEN
	STEP:=0;
END_IF;


CASE STEP OF

0: (*Init*)

	ST2_2V4 := FALSE;
	ST2_2V5 := FALSE;
	ST2_2V6 := FALSE;
	ST2_2V7 := FALSE;
	ST2_2V8 := FALSE;
	
	FLAGS.ST2_RELEASE_CARRIER := FALSE;
	START_TAMPO_AMORCAGE := FALSE;
	START_TAMPO_MESURE := FALSE;
	START_TAMPO_VERIN_ELEC := FALSE;
	START_TAMPO_VERIN_HAUT := FALSE;
	START_TAMPO_VERIN_BAS := FALSE;
	
	STEP := STEP + 10;
	
10: (*Attend une palette / flanc montant du status*)

	IF JIG_STATUS_R_TRIG.Q THEN
		STEP := STEP + 10;
	END_IF;
	
20: (*Descend le vérin du dessus*)

	ST2_2V4 := TRUE;
	START_TAMPO_VERIN_HAUT := TRUE;
	STEP := STEP + 10;
	
30: (*Attend vérin dessus sorti*)

	IF TAMPO_SORTIE_VERIN_HAUT_TON.Q THEN
		START_TAMPO_VERIN_HAUT := FALSE;
		STEP := STEP + 10;
	END_IF;

40: (*Monte le vérin du dessous*)

	ST2_2V5 := TRUE;
	START_TAMPO_VERIN_BAS := TRUE;
	STEP := STEP + 10;
	
50: (*Attend vérin dessus sorti*)

	IF TAMPO_SORTIE_VERIN_BAS_TON.Q THEN
		START_TAMPO_VERIN_BAS := FALSE;
		STEP := STEP + 10;
	END_IF;
	
60: (*Sort le vérin électrique*)

	ST2_2V6 := TRUE;
	START_TAMPO_VERIN_ELEC := TRUE;
	STEP := STEP + 10;
	
70: (*Attend vérin électrique sortie*)

	IF TAMPO_SORTIE_VERIN_ELEC_TON.Q THEN
		START_TAMPO_VERIN_ELEC := FALSE;
		STEP := STEP + 10;
	END_IF;

80: (*Allume la pompe*)

	IF PUMP_PARAMS.ST2_CONTRE_PRESSION THEN (*ouvre la vanne de contre pression d'air si nécessaire*)
		ST2_2V8:=TRUE;
	END_IF;
	
	ST2_POWER := TRUE;
	STEP := 100;
	
(**************************************************************************)
100: (*Démarre la tampo pour l'amorcage*)

	START_TAMPO_AMORCAGE := TRUE;
	STEP := STEP + 10;
	
110: (*Attend fin tampo amorcage*)

	IF TAMPO_AMORCAGE_TON.Q THEN
		START_TAMPO_AMORCAGE := FALSE;
		STEP := STEP + 10;
	END_IF;
	
120: (*Démarre la tampo pour la mesure de pression*)

	START_TAMPO_MESURE := TRUE;
	STEP := STEP + 10;
	
130: (*Attend fin tampo mesure - MESURE EN COURT*)

	IF TAMPO_MESURE_TON.Q THEN
		START_TAMPO_MESURE := FALSE;
		ST2_2V8:=FALSE; (*ferme la vanne de contre pression d'air*)
		STEP := STEP + 10;
	END_IF;
	
140: (*Démarre la tampo pour la mesure de la tenue aspiration*)

	START_TAMPO_MESURE_PERTES := TRUE;
	ST2_POWER := FALSE;
	STEP := STEP + 10;
	
150: (*Attend fin tampo mesure - MESURE EN COURT tenue aspiration*)

	IF TAMPO_MESURE_PERTES_TON.Q THEN
		START_TAMPO_MESURE_PERTES := FALSE;
		ST2_2V8:=FALSE; (*ferme la vanne de contre pression d'air*)
		STEP := 300;
	END_IF;
	

(**************************************************************************)	

300: (*Rentre le vérin électrique*)

	ST2_2V6 := FALSE;
	STEP := STEP + 10;
	
310: (*Descend le vérin du dessous*)

	ST2_2V5 := FALSE;
	STEP := STEP + 10;
	
320: (*Monte le vérin du dessus*)

	ST2_2V4 := FALSE;
	STEP := STEP + 10;

330: (*Libère la palette*)

	FLAGS.ST2_RELEASE_CARRIER := TRUE;
	STEP := 0;


ELSE
    (*default case*);
END_CASE;

(*FBs*)
TAMPO_SORTIE_VERIN_HAUT_TON(IN:= START_TAMPO_VERIN_HAUT, PT:= TEMPS_SORTIE_VERIN);
TAMPO_SORTIE_VERIN_BAS_TON(IN:= START_TAMPO_VERIN_BAS, PT:= TEMPS_SORTIE_VERIN);
TAMPO_SORTIE_VERIN_ELEC_TON(IN:= START_TAMPO_VERIN_ELEC, PT:= TEMPS_SORTIE_VERIN);

TAMPO_AMORCAGE_TON(IN:= START_TAMPO_AMORCAGE, PT:= PUMP_PARAMS.ST6_TEMPS_POMPAGE);
TAMPO_MESURE_TON(IN:= START_TAMPO_MESURE, PT:= PUMP_PARAMS.ST6_TEMPS_MESURE);
TAMPO_MESURE_PERTES_TON(IN:= START_TAMPO_MESURE_PERTES, PT:= PUMP_PARAMS.ST2_TEMPS_MESURE_PERTE_PRESSION);
JIG_STATUS_R_TRIG(CLK:= CONVEYOR.JIG_ST2.STATUS);
PROG_STEPS.ST2:=STEP;]]></ST>
    </Implementation>
    <ObjectProperties />
  </POU>
</TcPlcObject>