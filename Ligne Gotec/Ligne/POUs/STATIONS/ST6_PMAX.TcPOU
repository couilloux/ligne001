<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.0.26">
  <POU Name="ST6_PMAX" Id="{47e850ea-5ac9-4951-91db-3de3451b360c}">
    <Declaration><![CDATA[PROGRAM ST6_PMAX
VAR
	STEP: INT := 0;
	JIG_STATUS_R_TRIG: R_TRIG;
	START_TAMPO_AMORCAGE: BOOL;
	TAMPO_AMORCAGE_TON: TON;
	START_TAMPO_MESURE: BOOL;
	TAMPO_MESURE_TON: TON;
	START_TAMPO_VIDANGE: BOOL;
	TAMPO_VIDANGE_TON: TON;
	TAMPO_SORTIE_VERIN_HAUT_TON: TON;
	START_TAMPO_VERIN_HAUT: BOOL;
	TAMPO_SORTIE_VERIN_BAS_TON: TON;
	START_TAMPO_VERIN_BAS: BOOL;
	TAMPO_SORTIE_VERIN_ELEC_TON: TON;
	START_TAMPO_VERIN_ELEC: BOOL;
	START_TAMPO_MESURE_PERTES: BOOL;
	TAMPO_MESURE_PERTES_TON: TON;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[(*******************************************************
*
* Station 6 - Pression MAX
*
*
*********************************************************)
IF NOT(GEMMA.STEP='F1') THEN
	STEP:=0;
END_IF;


CASE STEP OF

0: (*Init*)

	ST6_6V4 := FALSE;
	ST6_6V5 := FALSE;
	ST6_6V6 := FALSE;
	ST6_6V6 := FALSE;
	ST6_6V7 := FALSE;
	ST6_6V10 := FALSE;
	ST6_6V11 := FALSE;
	
	FLAGS.ST6_RELEASE_CARRIER := FALSE;
	START_TAMPO_AMORCAGE := FALSE;
	START_TAMPO_MESURE:= FALSE;
	START_TAMPO_MESURE_PERTES:= FALSE;
	START_TAMPO_VIDANGE:= FALSE;
	START_TAMPO_VERIN_ELEC := FALSE;
	START_TAMPO_VERIN_HAUT := FALSE;
	START_TAMPO_VERIN_BAS := FALSE;
	
	STEP := STEP + 10;
	
10: (*Attend une palette / flanc montant du status*)

	IF JIG_STATUS_R_TRIG.Q THEN
		STEP := STEP + 10;
	END_IF;
	
20: (*Descend le vérin du dessus*)

	ST6_6V4 := TRUE;
	START_TAMPO_VERIN_HAUT := TRUE;
	STEP := STEP + 10;
	
30: (*Attend vérin dessus sorti*)

	IF TAMPO_SORTIE_VERIN_HAUT_TON.Q THEN
		START_TAMPO_VERIN_HAUT := FALSE;
		STEP := STEP + 10;
	END_IF;

40: (*Monte le vérin du dessous*)

	ST6_6V5 := TRUE;
	START_TAMPO_VERIN_BAS := TRUE;
	STEP := STEP + 10;
	
50: (*Attend vérin dessus sorti*)

	IF TAMPO_SORTIE_VERIN_BAS_TON.Q THEN
		START_TAMPO_VERIN_BAS := FALSE;
		STEP := STEP + 10;
	END_IF;
	
60: (*Sort le vérin électrique*)

	ST6_6V6 := TRUE;
	IF PUMP_PARAMS.GEN_BYPASS THEN
		ST6_6V7 := TRUE;
	END_IF
	START_TAMPO_VERIN_ELEC := TRUE;
	STEP := STEP + 10;
	
70: (*Attend vérin électrique sortie*)

	IF TAMPO_SORTIE_VERIN_ELEC_TON.Q THEN
		START_TAMPO_VERIN_ELEC := FALSE;
		STEP := STEP + 10;
	END_IF;

80: (*Allume la pompe*)

	ST6_POWER := TRUE;
	ST6_6V10 := TRUE;
	ST6_6V11 := TRUE;
	STEP := 100;
	
	
(**************************************************************************)
100: (*Démarre la tampo pour l'amorcage*)

	START_TAMPO_AMORCAGE := TRUE;
	STEP := STEP + 10;
	
110: (*Attend fin tampo amorcage*)

	IF TAMPO_AMORCAGE_TON.Q THEN
		START_TAMPO_AMORCAGE := FALSE;
		STEP := STEP + 10;
	END_IF;
	
120: (*Démarre la tampo pour la mesure de pression*)

	START_TAMPO_MESURE := TRUE;
	ST6_6V11 := FALSE; 		(*ferme la vanne après la pompe *)
	STEP := STEP + 10;
	
130: (*Attend fin tampo mesure - MESURE EN COURT*)

	IF TAMPO_MESURE_TON.Q THEN
		START_TAMPO_MESURE := FALSE;
		ST6_6V11 := TRUE; 		(*re-ouvre la vanne après la pompe *)
		STEP := STEP + 10;
	END_IF;
	
140: (*Démarre la tampo pour la mesure de perte de pression*)

	START_TAMPO_MESURE_PERTES := TRUE;
	ST6_POWER := TRUE;
	STEP := STEP + 10;
	
150: (*Attend fin tampo mesure - MESURE EN COURT perte de pression*)

	IF TAMPO_MESURE_PERTES_TON.Q THEN
		START_TAMPO_MESURE_PERTES := FALSE;
		STEP := 300;
	END_IF;
	

(**************************************************************************)	

300: (*Descend le vérin du dessous*)

	ST6_6V5 := FALSE;
	ST6_6V10 := FALSE;	(* ferme la vanne avant la pompe*)
	STEP := STEP + 10;

310: (*Démarre la tampo de vidange de la pompe*)

	START_TAMPO_VIDANGE := TRUE;
	STEP := STEP + 10;
	
320: (*Attend fin tampo amorcage*)

	IF TAMPO_VIDANGE_TON.Q THEN
		START_TAMPO_VIDANGE := FALSE;
		ST6_6V11 := FALSE; (* ferme la vanne après la pompe*)
		STEP := STEP + 10;
	END_IF;

330: (*Rentre le vérin électrique*)

	ST6_6V6 := FALSE;
	ST6_6V7 := FALSE;
	STEP := STEP + 10;
	
	
340: (*Monte le vérin du dessus*)

	ST6_6V4 := FALSE;
	STEP := STEP + 10;

350: (*Libère la palette*)

	FLAGS.ST6_RELEASE_CARRIER := TRUE;
	STEP := 0;


ELSE
    (*default case*);
END_CASE;

(*FBs*)
TAMPO_SORTIE_VERIN_HAUT_TON(IN:= START_TAMPO_VERIN_HAUT, PT:= TEMPS_SORTIE_VERIN);
TAMPO_SORTIE_VERIN_BAS_TON(IN:= START_TAMPO_VERIN_BAS, PT:= TEMPS_SORTIE_VERIN);
TAMPO_SORTIE_VERIN_ELEC_TON(IN:= START_TAMPO_VERIN_ELEC, PT:= TEMPS_SORTIE_VERIN);

TAMPO_AMORCAGE_TON(IN:= START_TAMPO_AMORCAGE, PT:= PUMP_PARAMS.ST6_TEMPS_POMPAGE);
TAMPO_VIDANGE_TON(IN:= START_TAMPO_VIDANGE, PT:= TEMPS_VIDANGE_POMPE);
TAMPO_MESURE_TON(IN:= START_TAMPO_MESURE, PT:= PUMP_PARAMS.ST6_TEMPS_MESURE);
TAMPO_MESURE_PERTES_TON(IN:= START_TAMPO_MESURE_PERTES, PT:= PUMP_PARAMS.ST6_TEMPS_MESURE_PERTE_PRESSION);
JIG_STATUS_R_TRIG(CLK:= CONVEYOR.JIG_ST6.STATUS);
PROG_STEPS.ST6:=STEP;]]></ST>
    </Implementation>
    <ObjectProperties />
  </POU>
</TcPlcObject>